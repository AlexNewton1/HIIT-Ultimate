apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.gms.google-services'  // Google Services plugin

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.softwareoverflow.hiit_trainer"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions({
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        })
    }
    buildTypes {
        debug {
            versionNameSuffix ".debug"
            resValue "string", "app_version", "Version ${defaultConfig.versionName}${versionNameSuffix}"
            resValue "string", "ad_id_banner", "\"ca-app-pub-3940256099942544/6300978111\""
            buildConfigField "String", "AD_INTERSTITIAL_WORKOUT_START", "\"ca-app-pub-3940256099942544/1033173712\""
            buildConfigField "String", "AD_INTERSTITIAL_WORKOUT_END", "\"ca-app-pub-3940256099942544/1033173712\""
            buildConfigField "String", "IAB_PUBLIC_KEY", hiit_iab_public_key
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue "string", "app_version", "Version ${defaultConfig.versionName}"
            resValue "string", "ad_id_banner", hiit_ads_banner
            buildConfigField "String", "AD_INTERSTITIAL_WORKOUT_START", hiit_ads_interstitial_start_workout
            buildConfigField "String", "AD_INTERSTITIAL_WORKOUT_END", hiit_ads_interstitial_end_workout
            buildConfigField "String", "IAB_PUBLIC_KEY", hiit_iab_public_key
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        dataBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.72"

    // Core
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Coroutines
    def coroutines_version = "1.3.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Material Design
    implementation "com.google.android.material:material:1.1.0"

    // Androidx Components
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.preference:preference:1.1.1"

    // Androidx Architecture Components
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    kapt "androidx.room:room-compiler:$room_version"
    kapt "com.android.databinding:compiler:3.1.4"

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // UI
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'io.github.yavski:fab-speed-dial:1.0.6'

    // Monetization
    implementation 'com.android.billingclient:billing-ktx:3.0.0'
    implementation 'com.google.android.gms:play-services-ads:19.2.0'
    implementation 'com.google.android.ads.consent:consent-library:1.0.8'

    // Firebase
    implementation 'com.google.firebase:firebase-analytics:17.4.3'

    // Testing
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13'
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

    // Debugging
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'
}
